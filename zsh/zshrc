autoload -U colors
colors

# Plugins
source $HOME/.zsh/plugin/syntax-highlighting/zsh-syntax-highlighting.zsh
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main root brackets)

# History
HISTFILE=$HOME/.zsh_history
HISTSIZE=10000
SAVEHIST=10000

setopt append_history
setopt extended_history
setopt hist_expire_dups_first
setopt hist_ignore_dups
setopt hist_ignore_space
setopt hist_verify
setopt inc_append_history
setopt share_history

# Completion
unsetopt menu_complete
unsetopt flowcontrol
setopt auto_menu
setopt complete_in_word
setopt always_to_end

zstyle ':completion:*' matcher-list 'm:{a-zA-z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*:*:*:*:*:' menu select
zstyle ':completion:*:*:*:*:processes' command "ps -u `whoami` -o pid,user,comm -w -W"
zstyle ':completion:*:cd:*' tag-order local-directories directory-stack path-directories
zstyle ':completion::complete:*' use-cache 1
zstyle ':completion::complete:*' cache-path $ZSH/cache/

# Version Control
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git svn hg
zstyle ':vcs_info:*' get-revision false
zstyle ':vcs_info:*' use-simple true
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr '%F{green}●'
zstyle ':vcs_info:*' unstagedstr '%F{yellow}●'
zstyle ':vcs_info:*' formats ' %F{cyan}[%b%c%u%F{cyan}]'
zstyle ':vcs_info:git*+set-message:*' hooks git-hooks

# function: hook for git vcs
function +vi-git-hooks() {
	if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]]; then
		# untracked files
		if [[ $(git status --porcelain | grep '??' 2> /dev/null) != '' ]]; then
			hook_com[unstaged]+='%F{red}●'
		fi
	fi
}

precmd() {
	vcs_info
}

# Alias
if [ -f "$HOME/.aliases" ]; then
	source ~/.aliases
fi

alias mv="nocorrect mv"
alias cp="nocorrect cp"
alias mkdir="nocorrect mkdir"

cd() {
	builtin cd "$@"; print "$fg[green]$PWD$reset_color"
}

pwd() {
	printf "$fg[green]$PWD$reset_color\n"
}

# Prompt
setopt prompt_subst
PROMPT='%F{blue}%c${vcs_info_msg_0_} %F{white}» %{$reset_color%}'
RPROMPT=''
