source $HOME/.zsh/bundle/antigen/antigen.zsh
ADOTDIR=$HOME/.zsh/bundle

antigen use oh-my-zsh

ZSH_TMUX_AUTOSTART=true
ZSH_TMUX_AUTOCONNECT=false
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets root)

antigen bundle command-not-found
antigen bundle per-directory-history
antigen bundle tmux
antigen bundle zsh-users/zsh-completions src
antigen bundle zsh-users/zsh-syntax-highlighting

antigen apply

ZLS_COLORS=$LS_COLORS

# Version Control
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git svn hg
zstyle ':vcs_info:*' get-revision false
zstyle ':vcs_info:*' use-simple true
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr '%F{green}●'
zstyle ':vcs_info:*' unstagedstr '%F{yellow}●'
zstyle ':vcs_info:*' formats ' %F{cyan}[ %b%c%u %F{cyan}]'
zstyle ':vcs_info:git*+set-message:*' hooks git-hooks

# function: hook for git vcs
function +vi-git-hooks() {
	if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]]; then
		# untracked files
		if [[ $(git status --porcelain | grep '??' 2> /dev/null) != '' ]]; then
			hook_com[unstaged]+='%F{red}●'
		fi
		# time since last commit
		last_commit=$(git log -1 --pretty=format:"%at")
		now=$(date +%s)
		seconds=$((now - last_commit))
		if [ "$seconds" -lt 60 ]; then
			time_passed="0m"
		else
			minutes=$((seconds / 60))
			hours=$((seconds / 3600))
			days=$((seconds / 86400))
			local time_passed=""
			if [ "$days" -gt 0 ]; then
				time_passed+=$days
				time_passed+="d"
				hours=$((hours % 24))
			fi
			if [ "$hours" -gt 0 ]; then
				time_passed+=$hours
				time_passed+="h"
				minutes=$((minutes % 60))
			fi
			if [ "$minutes" -gt 0 ] && [ "$days" -eq 0 ]; then
				time_passed+=$minutes
				time_passed+="m"
			fi
		fi
		hook_com[unstaged]+=" %F{yellow}$time_passed"
	fi
}

precmd() {
	vcs_info
}

# Aliases
alias egrep="egrep --color=auto"
alias fgrep="fgrep --color=auto"
alias grep="grep --color=auto"

# alias ls="ls --color=auto --group-directories-first"
# alias la="ls --color=auto --group-directories-first -C -AF"

alias mv="nocorrect mv"
alias cp="nocorrect cp"
alias mkdir="nocorrect mkdir"

cd() {
	builtin cd "$@"; print "$fg[green]$PWD$reset_color"
}

pwd() {
	printf "$fg[green]$PWD$reset_color\n"
}

# Prompt
setopt prompt_subst
PROMPT='%F{cyan}%c${vcs_info_msg_0_} %F{white}%% %{$reset_color%}'
RPROMPT=''
