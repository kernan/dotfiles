# @author Robert Kernan

# General -------------------------------------------------------------------{{{
# set terminal type
case "$TERM" in
	*-256color)
		# alias ssh='TERM=${TERM%-256color} ssh'
		alias ssh='TERM=linux ssh'
		;;
	*)
		POTENTIAL_TERM=${TERM}-256color
		POTENTIAL_TERMINFO=${TERM:0:1}/$POTENTIAL_TERM
		BOX_TERMINFO_DIR=/usr/share/terminfo
		[[ -f $BOX_TERMINFO_DIR/$POTENTIAL_TERMINFO ]] && \
			export TERM=$POTENTIAL_TERM
		HOME_TERMINFO_DIR=$HOME/.terminfo
		[[ -f $HOME_TERMINFO_DIR/$POTENTIAL_TERMINFO ]] && \
			export TERM=$POTENTIAL_TERM
		;;
esac
# disable XON/XOFF flow control
stty -ixon
# disable beep
setterm -bfreq 0
# config directory
ZSH_HOME="$HOME/.zsh"
# ignore duplicate lines
setopt histignoredups
# prevent file clobbering
setopt noclobber
# autocorrection
setopt correct
# colors
autoload colors; colors
# exports
export EDITOR="vim"
export VISUAL="vim"
# }}}

# Mode ----------------------------------------------------------------------{{{
# use vi keybindings
bindkey -v
# (some) emacs bindings in vim mode
bindkey '^e' end-of-line
bindkey '^a' beginning-of-line
bindkey '^h' backward-char
bindkey '^l' forward-char
bindkey '^p' up-history
bindkey '^n' down-history
# }}}

# Autocompletion ------------------------------------------------------------{{{
autoload -U compinit
compinit
setopt completealiases
zstyle ':completion:*' menu select
zstyle ':completion:*:descriptions' format '%U%B%d%b%u'
zstyle ':completion:*:warnings' format '%BNo matches for: %d%b'
zstyle ':completion:*:sudo:*' command-path /usr/local/sbin /usr/local/bin /usr/sbin /usr/bin /sbin /bin /usr/X11R6/bin
# }}}

# Version Control -----------------------------------------------------------{{{
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git svn hg
zstyle ':vcs_info:*' get-revision false
zstyle ':vcs_info:*' use-simple true
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr '%F{green}●'
zstyle ':vcs_info:*' unstagedstr '%F{yellow}●'
zstyle ':vcs_info:*' formats ' %F{cyan}[ %b%c%u'
zstyle ':vcs_info:git*+set-message:*' hooks git-hooks
# function: hook for git vcs {{{
function +vi-git-hooks() {
	if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]]; then
		# untracked files
		if [[ $(git status --porcelain | grep '??' 2> /dev/null) != '' ]]; then
			hook_com[unstaged]+='%F{red}●'
		fi
		# time since last commit
		last_commit=$(git log -1 --pretty=format:"%at")
		now=$(date +%s)
		seconds=$((now - last_commit))
		minutes=$((seconds / 60))
		hours=$((seconds / 3600))
		days=$((seconds / 86400))
		local time_passed=""
		if [ "$days" -gt 0 ]; then
			time_passed+=$days
			time_passed+="d"
			hours=$((hours % 24))
		fi
		if [ "$hours" -gt 0 ]; then
			time_passed+=$hours
			time_passed+="h"
			minutes=$((minutes % 60))
		fi
		if [ "$minutes" -gt 0 ] && [ "$days" -eq 0 ]; then
			time_passed+=$minutes
			time_passed+="m"
		fi
		hook_com[unstaged]+=" %F{cyan}] %F{yellow}%{$time_passed%}"
	fi
}
#}}}

precmd() {
	vcs_info
}
# }}}

# Aliases -------------------------------------------------------------------{{{
alias egrep="egrep --color=auto"
alias fgrep="fgrep --color=auto"
alias grep="grep --color=auto"

alias ls="ls --color=auto --group-directories-first"
alias la="ls --color=auto --group-directories-first -C -AF"
alias ll="ls --color=auto --group-directories-first -C -lA"

alias mv="nocorrect mv"
alias cp="nocorrect cp"
alias mkdir="nocorrect mkdir"

cd() {
	builtin cd "$@"; print "$fg[green]$PWD$reset_color"
}

pwd() {
	printf "$fg[green]$PWD$reset_color\n"
}

# }}}

# Highlighting --------------------------------------------------------------{{{
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)
source $ZSH_HOME/plugin/syntax-hi/zsh-syntax-highlighting.zsh
# }}}

# Prompt --------------------------------------------------------------------{{{
setopt prompt_subst
PROMPT='%F{cyan}%c${vcs_info_msg_0_} %F{white}%(!.#.⚡) %{$reset_color%}'
RPROMPT=''
# }}}
